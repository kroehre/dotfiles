# shortcut to this dotfiles path is $DOTHOME
export DOTHOME=$HOME/.dotfiles

# source folder that we can `scd [tab]` into
export SOURCE_DIR=$HOME/src

# all of our zsh files
typeset -U config_files
config_files=($DOTHOME/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# I want to use [ and ] damn it!
unsetopt nomatch

# set cd autocompletion to commonly visited directories
cdpath=(. ~ ~/src $SOURCE_DIR)

# chruby!
# osx location
[[ ! -a /usr/local/opt/chruby/share/chruby/chruby.sh ]] || source /usr/local/opt/chruby/share/chruby/chruby.sh
[[ ! -a /usr/local/opt/chruby/share/chruby/auto.sh ]] || source /usr/local/opt/chruby/share/chruby/auto.sh
# ubuntu location
[[ ! -a /usr/local/share/chruby/chruby.sh ]] || source /usr/local/share/chruby/chruby.sh
[[ ! -a /usr/local/share/chruby/auto.sh ]] || source /usr/local/share/chruby/auto.sh

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# gem_home
if [[ -a /usr/local/opt/gem_home/share/gem_home/gem_home.sh ]]; then
  source /usr/local/opt/gem_home/share/gem_home/gem_home.sh

  # Automatically set local Gem Home directory
  # when entering a directory that contains a Gemfile.
  unset GEM_HOME_AUTO

  function gem_home_auto() {
    local dir="${PWD}/"

    until [[ -z "$dir" ]]; do
      dir="${dir%/*}"
      if [[ -f "$dir/Gemfile" ]]; then
        if [[ "$dir" == "$GEM_HOME_AUTO" ]]; then return
        else
          GEM_HOME_AUTO="$dir"
          gem_home "$dir"
          return $?
        fi
      fi
    done

    if [[ -n "$GEM_HOME_AUTO" ]]; then
      gem_home -
      unset GEM_HOME_AUTO
    fi
  }

  if [[ ! "$preexec_functions" == *gem_home_auto* ]]; then
    preexec_functions+=("gem_home_auto")
  fi
fi

# import local zsh customizations, if present
zrcl="$HOME/.zshrc.local"

[[ ! -a $zrcl ]] || source $zrcl
